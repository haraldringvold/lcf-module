buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'http://repo.enonic.net/public'
        }
        maven {
            url 'http://dl.bintray.com/robfletcher/gradle-plugins'
        }
    }

    dependencies {
        classpath 'com.enonic.xp:gradle-plugin:6.0.0'
        classpath 'com.eriwen:gradle-js-plugin:1.12.1'
        classpath 'com.github.robfletcher:compass-gradle-plugin:2.0.4'
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
        classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.3.2'
    }
}


apply plugin: 'idea'
apply plugin: 'com.enonic.xp.app'
apply plugin: 'maven'
apply plugin: 'com.eriwen.gradle.js'
apply plugin: 'base'
apply plugin: 'com.github.robfletcher.compass'
apply plugin: 'org.hidetake.ssh'
apply plugin: 'pl.allegro.tech.build.axion-release'


app {
    name = 'no.ripples.xp.lcf'
    displayName = 'Link Child Foundation'
    vendorName = 'Ripples'
    vendorUrl = 'http://ripples.no'
}

ext {
    xpVersion = '6.0.0'
}

remotes {
  webServer {
    host = 'xp.harald.io'
    user = 'harald'
    identity = file('/Users/harald/.ssh/id_rsa')
  }
}


scmVersion {
    tag {
        prefix = rootProject.name
        initialVersion = {config, position -> '1.0.0'}
    }
}

project.version = scmVersion.version

task publish << {

  println "$rootProject.name-$scmVersion.version"+".jar"
  ssh.run {
    session(remotes.webServer) {
      put "$rootProject.name-$scmVersion.version"+".jar", '/opt/enonic-xp-6.0.0/home/deploy'
    }
  }
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
    include "com.enonic.xp:lib-xslt:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.enonic.lib:util:1.0.0"
    include 'com.enonic.lib:menu:1.0.0'
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://repo.enonic.com/public'
    }
}

compass {
    cssDir = file("src/main/resources/site/assets/css")
    sassDir = file("src/main/resources/site/assets/sass")
}

// Declare your sources
javascript.source {
    custom {
        js {
            srcDir "src/main/resources/site/assets/js"
            include "*.js"
            //exclude "*.min.js"
        }
    }
    vendor {
        js {
            srcDir "src/main/resources/site/assets/js/vendor"
            include "*.js"
        }
    }
    jquery {
            js {
                srcDir "src/main/resources/site/assets/js/vendor/jquery"
                include "*.js"
            }
        }
}

minifyJs {
    source = javascript.source.custom.js.files
    dest = file("${buildDir}/resources/main/site/assets/js/custom.min.js")
    closure {
        warningLevel = 'QUIET'
    }
}


// Configure the built-in task
combineJs {
    source = javascript.source.jquery.js.files + javascript.source.vendor.js.files + minifyJs
    dest = file("${buildDir}/resources/main/site/assets/js/app.js")
}

jar.dependsOn += combineJs